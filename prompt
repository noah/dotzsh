# Global colors
for COLOR in CYAN MAGENTA BLUE RED GREEN YELLOW WHITE BLACK; do
    eval PR_BRIGHT_$COLOR='%{$fg[${(L)COLOR}]%}'
    eval PR_BOLD_$COLOR='%{$fg_bold[${(L)COLOR}]%}'
done
PR_RESET="%{$reset_color%}"
SEP="  "

# Display vi mode in prompt
#       See: http://zshwiki.org/home/examples/zlewidgets

function zle-line-init zle-keymap-select {
    VI_MODE="${${KEYMAP/vicmd/${PR_BOLD_RED}-- NORMAL --${PR_RESET}}/(main|viins)/${PR_BOLD_GREEN}-- INSERT --${PR_RESET}}"
    zle reset-prompt
}
zle -N zle-line-init
zle -N zle-keymap-select

## A custom prompt that looks like this:
#
# !4386  +0  *noah  @downbe  .20100313 16:24:44 CST  ^0
# %/# 
#
# The value prefixes are quasi-mnemonic.  E.g.,
#   !n runs the command with history number n
#   + is displayed in output of jobs command e.g., 
#       $ jobs
#       [1]  + suspended  vim
#   * of course, the user is represented by a star
#   @ hostname 
#   . dot for where we are right now (in terms of time)
#   ^ for last job
#
# history num
# num jobs
# user
# host
# timestamp
# exit status of last command

PROMPT='\
!${PR_BOLD_CYAN}%!${PR_RESET}$SEP\
+${PR_BOLD_YELLOW}%j${PR_RESET}$SEP\
*${PR_BOLD_GREEN}%n${PR_RESET}$SEP\
@${PR_BOLD_MAGENTA}%M${PR_RESET}$SEP\
.${PR_BOLD_BLUE}%D{%Y%m%d %T %Z}${PR_RESET}$SEP\
^${PR_BOLD_RED}%?${PR_RESET}$SEP\
${VI_MODE}${PR_RESET}
%/
%# '

# vim: ft=zsh
